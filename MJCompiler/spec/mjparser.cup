

package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	Logger log = Logger.getLogger(getClass());
	
	/******* parser error handling ********/
	
	boolean errorDetected;
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      errorDetected = true;
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
    	errorDetected = true;
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
    	errorDetected = true;
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }

	/***************************************/
:}

init with {:
	errorDetected = false;
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

/******************* terminals ***********************/

terminal PROGRAM, BREAK, CLASS, ENUM, ELSE, CONST, IF, DO, WHILE, NEW, PRINT, READ, RETURN, VOID, EXTENDS, CONTINUE, FOREACH;

terminal String IDENT;
terminal Integer NUM_CONST;
terminal Character CHAR_CONST;
terminal Boolean BOOL_CONST;

terminal PLUS, MINUS, MUL, DIV, PERCENT, DEQ, NE, GT, GE, LT, LE, AND, OR, EQ, INC, DEC;
terminal SEMICOLON, COLON, COMMA, DOT, LPAREN, RPAREN, LSQUARE, RSQUARE, LBRACE, RBRACE, LAMBDA;

/******************* nonterminals **********************/

/* Program */
nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgramName, Program, GlobalMethodDecl_Void, GlobalMethodDecl_Ident;
nonterminal GlobalDeclList, GlobalMethodDeclList;

/* ConstDecl */
nonterminal ConstDecl, OtherConstDeclarations;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Constant;

/* VarDecl */
nonterminal VarDecl, NewVarDecl, OptionalVarDeclList, OptionalSquare;
nonterminal Class_VarDecl, Class_NewVarDecl, Class_OptionalVarDeclList;
nonterminal Global_VarDecl, Global_NewVarDecl, Global_OptionalVarDeclList;

/* ClassDecl */
nonterminal ClassDecl, ClassBody, Dummy_ClassBody, Class_VarDeclList, ConstructorMethodLists, ClassMethodConstructorLists, MethodDeclList;
nonterminal ConstructorDeclList;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ClassNameAndDerivation, ConstructorDecl_Start;
nonterminal rs.etf.pp1.symboltable.concepts.Obj MethodDecl_Void, MethodDecl_Ident;
nonterminal rs.etf.pp1.symboltable.concepts.Struct OptionalDerivation;

/* ConstructorDecl */
nonterminal ConstructorDecl, FormParsOptional, StatementList, VarDeclList;

/* MethodDecl */
nonterminal rs.etf.pp1.symboltable.concepts.Obj MethodDecl;

/* FormPars */
nonterminal FormPars, TypeIdentList;
nonterminal rs.etf.pp1.symboltable.concepts.Obj OneFormPar;

/* Type */
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type;

/* Statement */
nonterminal Statement, Unmatched, Matched;
nonterminal WhileLoopStart;
nonterminal rs.etf.pp1.symboltable.concepts.Struct ReturnOptionalExpr, DesignatorForeach, PrintNumConst;
nonterminal rs.etf.pp1.symboltable.concepts.Obj CurrVarDesignator; 
nonterminal OpenIfElseScope, PatchOrConditionJumps, ExitIfBlock_PatchJumpsToElse;

/* DesignatorStatement */
nonterminal DesignatorStatement, OptionalDesignatorList, OptionalActPars, OptionalDesignator, DesignatorList;
nonterminal PrintStart;
		 
/* ActPars */
nonterminal ActPars, ExprList;
nonterminal rs.etf.pp1.symboltable.concepts.Struct ActPar;

/* Condition */
nonterminal Condition, ConditionIf, ConditionWhile, Or_CondTermList;
nonterminal PatchAndConditionJumps;

/* CondTerm */
nonterminal And_CondFactList;
nonterminal rs.etf.pp1.symboltable.concepts.Struct CondTerm;

/* CondFact */
nonterminal rs.etf.pp1.symboltable.concepts.Struct CondFact;

/* Expr */
nonterminal rs.etf.pp1.symboltable.concepts.Struct Expr;

/* Term */
nonterminal rs.etf.pp1.symboltable.concepts.Struct Term;

/* Factor */
nonterminal rs.etf.pp1.symboltable.concepts.Obj CalledConstructorName;
nonterminal CalledFunctionOrMethodDesignator;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Factor;

/* Designator */
nonterminal rs.etf.pp1.symboltable.concepts.Obj Designator;
nonterminal ArrayTypeDesignator;

/* Label */
nonterminal Label;

/* Assignop */
nonterminal Assignop;

/* Relop */
nonterminal Relop;

/* Addop */
nonterminal Addop;

/* Mulop */
nonterminal Mulop;

/****************** precedence ************************/

precedence left ELSE;

/************************ grammar **********************/

/***** Program ******/

Program ::= (Program_) PROGRAM ProgramName GlobalDeclList LBRACE GlobalMethodDeclList RBRACE;

ProgramName ::= (ProgramName) IDENT:programName;

GlobalDeclList ::= (GlobalDeclListConst) 	GlobalDeclList ConstDecl 
				 | (GlobalDeclListVar) 		GlobalDeclList Global_VarDecl   
				 | (GlobalDeclListClass) 	GlobalDeclList ClassDecl 
				 | (EmptyGlobalDeclList) 	/* empty */
				 ;
				  
GlobalMethodDeclList ::= (GlobalMethodDeclList_Void) 	GlobalMethodDeclList GlobalMethodDecl_Void LPAREN MethodDecl 
					   | (GlobalMethodDeclList_Ident) 	GlobalMethodDeclList GlobalMethodDecl_Ident LPAREN MethodDecl
				   	   | (EmptyGlobalMethodDeclList) 	/* empty */
				   	   ;
				   	   
GlobalMethodDecl_Void ::= (GlobalMethodDecl_Void) VOID IDENT:methodName;	

GlobalMethodDecl_Ident ::= (GlobalMethodDecl_Ident) Type IDENT:methodName;			   	   
				   	   
/***** ConstDecl *******/		   
				   
ConstDecl ::= (ConstDecl) CONST Type IDENT:firstIdentName EQ Constant OtherConstDeclarations SEMICOLON ;

Constant ::= 	(NumConstant) 	NUM_CONST:value
		   	|   (CharConstant) 	CHAR_CONST:value
			|   (BoolConstant) 	BOOL_CONST:value
			;
			   
OtherConstDeclarations ::= 	(OtherConstDeclarations_) 	OtherConstDeclarations COMMA IDENT:identName EQ Constant 
						|	(NoOtherConstDeclatations) 	/* empty */
						;

/******** VarDecl **********/
				   
VarDecl ::= (VarDecl) Type NewVarDecl OptionalVarDeclList SEMICOLON;

NewVarDecl ::= (NewVarDecl_) IDENT:varName OptionalSquare;

				 
OptionalVarDeclList ::= (OptionalVarDeclList_) 		OptionalVarDeclList COMMA NewVarDecl
					 | 	(EmptyOptionalVarDeclList) 	/* empty */
					 ;
					 
Global_VarDecl ::= (Global_VarDecl) Type Global_NewVarDecl Global_OptionalVarDeclList SEMICOLON;

Global_NewVarDecl ::= (Global_NewVarDecl_) 		IDENT:varName OptionalSquare
			|  (ERROR_NewGlobalVarDecl) 	error:l {: parser.report_error("Greska pri deklaraciji globalne promenljive u liniji: " + lleft, null); :}
			;

				 
Global_OptionalVarDeclList ::= (Global_OptionalVarDeclList_) 		Global_OptionalVarDeclList COMMA Global_NewVarDecl
					 | 	(Global_EmptyOptionalVarDeclList) 	/* empty */
					 |	(ERROR_OptinalGlobalVarDeclList)	error:l {: parser.report_error("Greska pri deklaraciji globalne promenljive u liniji: " + lleft, null); :}
					 ;
					 
OptionalSquare ::= (OptionalSquare_) 	LSQUARE RSQUARE
				|  (NoSquare) 			/* empty */
				;
				
/* class variable declaration (error handling) */
/* note that COMMA is not a safe symbol */

Class_VarDecl ::= (Class_VarDecl_) 		Type Class_NewVarDecl Class_OptionalVarDeclList SEMICOLON
				| (ERROR_Class_VarDecl)	error:l {: parser.report_error("Greska pri deklarisanju polja klase u liniji " + lleft, null); :}
				;	

Class_NewVarDecl ::= (Class_NewVarDecl) IDENT:varName OptionalSquare;

Class_OptionalVarDeclList ::= 	(Class_OptionalVarDeclList_) 		Class_OptionalVarDeclList COMMA Class_NewVarDecl
					 		| 	(Class_EmptyOptionalVarDeclList) 	/* empty */
							;

/*********** ClassDecl **********/

ClassDecl ::= (ClassDecl) ClassNameAndDerivation LBRACE ClassBody RBRACE;

ClassNameAndDerivation ::= (ClassNameAndDerivation) CLASS IDENT:className OptionalDerivation;

ClassBody ::= (ClassBody) Class_VarDeclList Dummy_ClassBody ConstructorMethodLists;

Dummy_ClassBody ::= (Dummy_ClassBody_) /* empty */;

Class_VarDeclList ::= 	(Class_VarDeclList_) 		Class_VarDeclList Class_VarDecl
					| 	(Class_EmptyVarDeclList) 	/* empty */
					;

OptionalDerivation ::= 	(OptionalDerivation_) 	EXTENDS Type
					|	(NoDerivation) 			/* empty */
					|	(ClassDerivation) 		error:l {: parser.report_error("Greska pri parsiranju deklaracije prosirenja natklase u liniji: " + lleft, null); :}
					;
				
ConstructorMethodLists ::= 	(ConstructorMethodLists_) 		LBRACE ClassMethodConstructorLists RBRACE 
						|	(EmptyConstructorMethodLists) 	/* empty */
						;

/* resolving method-constructor parsing conflict  */
ClassMethodConstructorLists ::= (ConstructorsAndMethods_Ident) 	ConstructorDecl_Start ConstructorDeclList MethodDecl_Ident LPAREN MethodDeclList 
							 | 	(ConstructorsAndMethods_Void) 	ConstructorDecl_Start ConstructorDeclList MethodDecl_Void LPAREN MethodDeclList 
							 | 	(Constructors) 					ConstructorDecl_Start ConstructorDeclList 
							 | 	(Methods_Ident) 				MethodDecl_Ident LPAREN MethodDeclList
							 | 	(Methods_Void) 					MethodDecl_Void LPAREN MethodDeclList
							 | 	(EmptyClassMethodConstructorLists) /* empty */
							 ;
							 
ConstructorDecl_Start ::= (ConstructorDecl_Start) IDENT:constructorName LPAREN;

MethodDeclList ::= (MethodDeclList_Ident) 	MethodDeclList MethodDecl_Ident LPAREN MethodDecl
				 | (MethodDeclList_Void) 	MethodDeclList MethodDecl_Void LPAREN MethodDecl
				 | (OneMethodDecl) 			MethodDecl
				 ;

ConstructorDeclList ::= (ConstructorDeclList_) 	ConstructorDeclList ConstructorDecl_Start ConstructorDecl
					  |	(NoConstructorDeclList) ConstructorDecl
					  ;
					  
MethodDecl_Void ::= (MethodDecl_Void) VOID IDENT:methodName;	

MethodDecl_Ident ::= (MethodDecl_Ident) Type IDENT:methodName;	
						
/****** ConstructorDecl *****/

/* note that IDENT LPAREN is already parsed */
ConstructorDecl ::= (ConstructorDecl) FormParsOptional RPAREN VarDeclList LBRACE StatementList RBRACE;

VarDeclList ::= (VarDeclList_) 				VarDeclList VarDecl
			| 	(EmptyClassVarDeclList) 	/* empty */
			;

FormParsOptional ::= 	(FormPars_) 	FormPars
					| 	(NoFormPars) 	/* empty */
					;
					
StatementList ::= (StatementList_) 		StatementList Statement
				| (EmptyStatementList) 	/* emptt */
				;

/****** MethodDecl **********/

/* note that IDENT/VOID IDENT LPAREN is already parsed */
MethodDecl ::= (MethodDecl) FormParsOptional RPAREN VarDeclList LBRACE StatementList RBRACE; 

/************ FormPars ********/

FormPars ::= (FormPars) OneFormPar TypeIdentList;

OneFormPar ::= 	(OneFormPar_) Type IDENT:formParName OptionalSquare
			|	(ERROR_OneFormPar) error:l {: parser.report_error("Greska pri deklaraciji formalnog parametra funkcije u liniji " + lleft, null); :}
			;
					
TypeIdentList ::= (TypeIdentList_) 		TypeIdentList COMMA OneFormPar
				| (EmptyTypeIdentList) 	/* empty */
				;
				  
/******** Type *****/

Type ::= (Type) IDENT:typeName;

/******** Statement ********/

Statement ::= (Matched_) Matched
			| (Unmatched_) Unmatched
			;
			  
Unmatched ::= (U_If) 		IF OpenIfElseScope LPAREN ConditionIf PatchOrConditionJumps RPAREN Statement
			| (U_IfElse) 	IF OpenIfElseScope LPAREN ConditionIf PatchOrConditionJumps RPAREN Matched ExitIfBlock_PatchJumpsToElse Unmatched
			;

Matched ::= (M_Designator) 	DesignatorStatement
		|	(M_IfElse) 		IF OpenIfElseScope LPAREN ConditionIf PatchOrConditionJumps RPAREN Matched ExitIfBlock_PatchJumpsToElse Matched 
		|	(M_While) 		WhileLoopStart ConditionWhile RPAREN Statement 
		|	(M_Break) 		BREAK SEMICOLON 
		|	(M_Continue) 	CONTINUE SEMICOLON 
		|	(M_Return) 		RETURN ReturnOptionalExpr SEMICOLON 
		|	(M_Read) 		READ LPAREN Designator RPAREN SEMICOLON 
		|	(M_Print) 		PrintStart PRINT LPAREN Expr PrintNumConst RPAREN SEMICOLON 
		|	(M_Foreach) 	DesignatorForeach CurrVarDesignator LAMBDA Statement RPAREN SEMICOLON 
		|	(M_Block) 		LBRACE StatementList RBRACE
		;
		
		
OpenIfElseScope ::= (OpenIfElseScope) /* empty */ ;
PatchOrConditionJumps ::= (PatchOrConditionJumps) /* empty */ ;
ExitIfBlock_PatchJumpsToElse ::= (ExitIfBlock_PatchJumpsToElse) ELSE ;
		
PrintStart ::= (PrintStart) /* empty */ ;
		
WhileLoopStart ::= (WhileLoopStart) WHILE LPAREN;	

DesignatorForeach ::= (DesignatorForeach) Designator DOT FOREACH LPAREN;

CurrVarDesignator ::= (CurrVarDesignator) IDENT:currVar;
				 
ReturnOptionalExpr ::= (ReturnOptionalExpr_) 	Expr 
			|	 (NoReturnExpr) 			/* empty */
			;
				 
PrintNumConst ::= (PrintNumConst_) 	COMMA NUM_CONST:printWidth 
				| (NoPrintNumConst) /* empty */
				;

/************** DesignatorStatement ***************/

DesignatorStatement ::= (DesignatorAssignment)		Designator Assignop Expr SEMICOLON
					|	(DesignatorFunctionCall)	CalledFunctionOrMethodDesignator LPAREN OptionalActPars RPAREN SEMICOLON
					|	(DesignatorInc)				Designator INC SEMICOLON
					|	(DesignatorDec)				Designator DEC SEMICOLON
					|	(ReverseArrayAssignment)    LSQUARE OptionalDesignatorList RSQUARE EQ Designator SEMICOLON 
					|	(ERROR_DesignatorStatement) error SEMICOLON:l {: parser.report_error("Greska pri konstrukciji iskaza dodele u liniji " + lleft, null); :}
					;
			
OptionalDesignatorList ::= OptionalDesignator DesignatorList;

OptionalActPars ::= (OptionalActPars_) 	ActPars
				 |  (NoOptionalActPars) /* empty */
				 ;
				    
OptionalDesignator ::= (OptionalDesignator_) 	Designator
					 | (NoOptionalDesignator) 	/* empty */
					 ;
					   
DesignatorList ::= (DesignatorList_) 		DesignatorList COMMA OptionalDesignator 
				 | (EmptyDesignatorList) 	/* empty */
				 ;

/******* ActPars ********/

ActPars ::= (ActPars) ActPar ExprList;

ExprList ::= (ExprList_) 		ExprList COMMA ActPar
		|	 (EmptyExprList) 	/* empty */
		;

ActPar ::= (ActPar) Expr;

/******* Condition *********/

Condition ::= (Condition) 			CondTerm Or_CondTermList;

ConditionIf ::= (ConditionIf_) 		CondTerm Or_CondTermList
			|	(ERROR_ConditionIf) error:l {: parser.report_error("Greska pri parsiranju logickog izraza unutar IF konstrukcije u liniji: " + lleft, null); :}
			;
			
ConditionWhile ::= (ConditionWhile) CondTerm Or_CondTermList;

Or_CondTermList ::= (OrCondTermList) 		Or_CondTermList PatchAndConditionJumps OR CondTerm 
				 |  (EmptyOrCondTermList) 	/* empty */
				 ;
				 
PatchAndConditionJumps ::= (PatchAndConditionJumps) /* empty */ ;

/******** CondTerm *********/

CondTerm ::= (CondTerm) CondFact And_CondFactList;

And_CondFactList ::= (AndCondFactList) 		And_CondFactList AND CondFact 
				  |	 (EmptyAndCondFactList) /* empty */
				  ;

/********* CondFact **********/

/* zanimljivo: nema shift/reduce konflikta */
CondFact ::= (CondFact_Expr) 		Expr
			|(CondFact_RelopExpr) 	Expr:leftExpr Relop Expr:rightExpr
			;

/******** Expr *********/

Expr ::= (Expr_Term)	Term
	  |	 (Expr_NegTerm)	MINUS Term
	  |	 (Expr_AddTerm)	Expr Addop Term
	  ;
 				  
/*********** Term *********/

Term ::= (Term_Factor) Factor
	  |  (Term_MulFactor) Term Mulop Factor
	  ;

/*********** Factor **********/

Factor ::= 	(F_DesignatorFuncCall)  CalledFunctionOrMethodDesignator LPAREN OptionalActPars RPAREN
		|	(F_Designator)			Designator 
		|   (F_NumConst) 			NUM_CONST:constValue 
		|   (F_CharConst) 			CHAR_CONST:constValue 
		|   (F_BoolConst) 			BOOL_CONST:constValue
		|   (F_NewObjConstruction) 	NEW CalledConstructorName LPAREN OptionalActPars RPAREN
		|	(F_NewArray)			NEW Type LSQUARE Expr RSQUARE 
		|   (F_Expr) 				LPAREN Expr RPAREN
		;

CalledConstructorName ::= (CalledConstructorName) Type;

CalledFunctionOrMethodDesignator ::= (CalledFunctionOrMethodDesignator) Designator;

/************* Designator ************/

Designator ::= (Designator_Ident) 		IDENT:designatorIdentName
			|  (Designator_FieldAccess) Designator DOT IDENT:fieldName
			|  (Designator_Indexing) 	ArrayTypeDesignator LSQUARE Expr:indexExpr RSQUARE
			;
			
ArrayTypeDesignator ::= (ArrayTypeDesignator) Designator;
				  
/************* Label ************/

Label ::= (Label) IDENT;

/********** Assignop **********/

Assignop ::= (Assignop) EQ;

/************** Relop ***************/

Relop ::= (R_Deq) 	DEQ 
	|	  (R_Ne) 	NE 
	|	  (R_Gt) 	GT 
	|	  (R_Ge) 	GE 
	|	  (R_Lt) 	LT 
	|	  (R_Le) 	LE
	;
		  
/*********** Addop **********/

Addop ::= (A_Plus) 	PLUS
		| (A_Minus) MINUS
		;
		  
/*********** Mulop **********/

Mulop ::= (M_Mul) 		MUL 
		| (M_Div) 		DIV 
		| (M_Percent) 	PERCENT
		;